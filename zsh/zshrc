###############################################################################
## This file loads the various config files to make editing easier and       ##
## cleaner. Also sets the global zsh options.                                ##
###############################################################################

### global-env directory ######################################################
export GLOBAL_ENV="$(dirname $(readlink ~/.zshrc))"



### Configuration files #######################################################
source ${GLOBAL_ENV}/func      # loads zsh functions
source ${GLOBAL_ENV}/env       # loads the enviroment
source ${GLOBAL_ENV}/aliases   # loads the general aliases
source ${GLOBAL_ENV}/hosts     # loads host aliases
source ${HOME}/.z${USER}           # loads user aliases



### Modules ###################################################################
zmodload zsh/mathfunc 
zmodload -a zsh/stat stat
zmodload -a zsh/zpty zpty
zmodload -a zsh/zprof zprof
zmodload -ap zsh/mapfile mapfile
zmodload zsh/complist



### Autocomplete extras #######################################################
compctl -k ping telnet ncftp host nslookup ssh
compctl -g '*.(gz|z|Z|t[agp]z|tarZ|tz)' + -g '*(-/)' gunzip gzcat zcat
compctl -g '*.(pdf|PDF)' + -g '*(-/)'  xpdf evince
compctl -g '[^.]*(-/) *(*)' + -f strip ldd gdb
compctl -j -P % -x 's[-] p[1]' -k signals -- kill



### Keybindings ###############################################################
# Create a zkbd compatible hash.
# To add other keys to this hash, see: man 5 terminfo
typeset -g -A key

key[Home]="${terminfo[khome]}"
key[End]="${terminfo[kend]}"
key[Insert]="${terminfo[kich1]}"
key[Backspace]="${terminfo[kbs]}"
key[Delete]="${terminfo[kdch1]}"
key[Up]="${terminfo[kcuu1]}"
key[Down]="${terminfo[kcud1]}"
key[Left]="${terminfo[kcub1]}"
key[Right]="${terminfo[kcuf1]}"
key[PageUp]="${terminfo[kpp]}"
key[PageDown]="${terminfo[knp]}"
key[Shift-Tab]="${terminfo[kcbt]}"

# Setup keys accordingly
[[ -n "${key[Home]}"      ]] && bindkey -- "${key[Home]}"       beginning-of-line
[[ -n "${key[End]}"       ]] && bindkey -- "${key[End]}"        end-of-line
[[ -n "${key[Insert]}"    ]] && bindkey -- "${key[Insert]}"     overwrite-mode
[[ -n "${key[Backspace]}" ]] && bindkey -- "${key[Backspace]}"  backward-delete-char
[[ -n "${key[Delete]}"    ]] && bindkey -- "${key[Delete]}"     delete-char
[[ -n "${key[Up]}"        ]] && bindkey -- "${key[Up]}"         up-line-or-history
[[ -n "${key[Down]}"      ]] && bindkey -- "${key[Down]}"       down-line-or-history
[[ -n "${key[Left]}"      ]] && bindkey -- "${key[Left]}"       backward-char
[[ -n "${key[Right]}"     ]] && bindkey -- "${key[Right]}"      forward-char
[[ -n "${key[PageUp]}"    ]] && bindkey -- "${key[PageUp]}"     beginning-of-buffer-or-history
[[ -n "${key[PageDown]}"  ]] && bindkey -- "${key[PageDown]}"   end-of-buffer-or-history
[[ -n "${key[Shift-Tab]}" ]] && bindkey -- "${key[Shift-Tab]}"  reverse-menu-complete

# Finally, make sure the terminal is in application mode, when zle is active.
# Only then are the values from $terminfo valid.
if (( ${+terminfo[smkx]} && ${+terminfo[rmkx]} )); then
	autoload -Uz add-zle-hook-widget
	function zle_application_mode_start { echoti smkx }
	function zle_application_mode_stop { echoti rmkx }
	add-zle-hook-widget -Uz zle-line-init zle_application_mode_start
	add-zle-hook-widget -Uz zle-line-finish zle_application_mode_stop
fi

bindkey '^R' history-incremental-search-backward



### Shell options #############################################################
setopt   notify globdots correct pushdtohome cdablevars autolist
setopt   correctall autocd recexact longlistjobs glob extendedglob
setopt   autoresume pushdsilent noclobber interactiveComments
setopt   autopushd pushdminus rcquotes
unsetopt bgnice autoparamslash
setopt    nobeep ignoreeof histignorealldups histignorespace PUSHD_IGNORE_DUPS
setopt   GlobComplete ListPacked ListTypes HistFindNoDups HistReduceBlanks
setopt   SunKeyboardHack multios list_packed



### Autoloads ################################################################
autoload -U compinit
compinit -u
autoload -U promptinit
promptinit
autoload zrecompile
autoload -U zmv
autoload colors ; colors



### Zstyle misc ###############################################################
zstyle ':completion:*::::' completer _expand _complete _ignored _approximate
zstyle ':completion:*:expand:*' tag-order all-expansions
zstyle ':completion:*' verbose yes
zstyle ':completion:*:descriptions' format '%B%d%b'
zstyle ':completion:*:messages' format '%d'
zstyle ':completion:*:warnings' format 'No matches for: %d'
zstyle ':completion:*:corrections' format '%B%d (errors: %e)%b'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters
zstyle '*' single-ignored show
zstyle -e ':completion:*:approximate:*' max-errors \
    'reply=( $(( ($#PREFIX+$#SUFFIX)/3 )) numeric )'
zstyle -e ':completion:*:sudo:*' command-path 'reply=($path)' 



### Misc ######################################################################
cdpath=(.. ~)

# Sysctl completion:
listsysctls () { set -A reply $(sysctl -AN ${1%.*}) }
compctl -K listsysctls sysctl

# Automatically remove duplicates from these arrays:
typeset -U path cdpath fpath manpath

# Compatibility with csh:
setenv() { typeset -x "${1}${1:+=}${(@)argv[2,$#]}" }

# Prevent core files from being written at all:
umask 027
ulimit -c 0

# Dircolors (if available):
[[ $(command -v dircolors) ]] && eval "$(dircolors)"

# Do not close on eof:
setopt ignoreeof



### Pywall support ############################################################
[[ -f ${HOME}/.cache/wal/sequences ]] && (cat ${HOME}/.cache/wal/sequences &)



### Custom theme ##############################################################
if [[ ${DISPLAY} ]]; then
  source ${GLOBAL_ENV}/theme 8bit
else
  source ${GLOBAL_ENV}/theme
fi

